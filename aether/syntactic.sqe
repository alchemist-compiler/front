Grammar(start):
    Production {Production} ;

Production:
    ProductionHeader ProductionBody ;

ProductionHeader:
    Identifier [Condition]@lexical [ProductionSpecifiers] [ProductionPrecedences]@lexical Colon ;

ProductionBody:
    SelectionExpression Semicolon ;

Condition:
    CommercialAt DisjunctionCondition ;

ProductionSpecifiers:
    LeftParenthesis ProductionSpecifierList RightParenthesis ;

ProductionPrecedences:
    GreaterThanSign ProductionPrecedenceList ;

SelectionExpression:
    SequenceExpression {VerticalLine SequenceExpression} ;

DisjunctionCondition:
    ConjunctionCondition {DoubleVerticalLine ConjunctionCondition} ;

ProductionSpecifierList:
    ProductionSpecifier {Comma ProductionSpecifier} ;

ProductionPrecedenceList:
    ProductionPrecedence {Comma ProductionPrecedence} ;

SequenceExpression:
    (IterationExpression {IterationExpression})@lexical |
    (PrimaryExpression {PrimaryExpression})@syntactic ;

ConjunctionCondition:
    NegationCondition {DoubleAmpersand NegationCondition} ;

ProductionSpecifier:
    (Identifier | (Discard | Optional) [Condition])@lexical |
    ((Start | Unambiguous) [Condition])@syntactic ;

ProductionPrecedence:
    Identifier ;

IterationExpression:
    (PrimaryExpression [Asterisk | PlusSign | QuestionMark | ExpressionRange])@lexical |
    (LeftCurlyBracket SelectionExpression RightCurlyBracket)@syntactic ;

PrimaryExpression:
    (OrdChar | QuotedChar | FullStop | BracketExpression)@lexical |
    (Identifier [Condition])@syntactic |
    LeftParenthesis SelectionExpression RightParenthesis [Condition]@syntactic |
    ((OptionalExpression | IterationExpression) [Condition])@syntactic ;

NegationCondition:
    {ExclamationMark} PrimitiveCondition ;

OptionalExpression:
    LeftSquareBracket SelectionExpression RightSquareBracket ;

PrimitiveCondition:
    Identifier
    LeftParenthesis DisjunctionCondition RightParenthesis ;
